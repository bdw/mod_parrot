namespace apache {
    class output {
        var request;
        var buffer;
        var contentWriter;
        var headerWriter;
        var statusWriter;
        var bytesWritten; // content bytes only
        
        function output(var request) {
            self.init(request);
        }
        
        function init(var request) {
            // for now, keep the auto-opening
            self.contentWriter = dlfunc(null, "mod_parrot_write", "ipip");
            self.headerWriter = dlfunc(null, "mod_parrot_header_out", "vPPPp");
            self.statusWriter = dlfunc(null, "mod_parrot_set_status", "vpi");
            self.buffer = new ByteBuffer; // warn then, I dare you
            self.open(request);
        }

        function open(var request) {
            self.request = request;
            self.bytesWritten = 0;
        }

        /* i split these functions because winxed does not have an easy way
         * to call super() methods. puts() is the entry point for the C
         * layer, write() for subclasses */
        function puts(string msg) {
            return self.write(msg);
        }

        function write(string msg) {
            var writer = self.contentWriter;
            self.buffer =: msg;
            int b = writer(self.buffer, elements(self.buffer), self.request);
            self.bytesWritten += b; // should check if this is larger than 0
            return b;
        }   

        function status(int val) {
            var writer = self.statusWriter;
            writer(self.request, val);
        }

        function header(string key, string value) {
            var writer = self.headerWriter;
            writer(getinterp(), key, value, self.request);
        }

    }   
    
    class input {
        var contentReader;
        var headerReader;
        var requestReader;
        var setup;
        var request;
        var remaining;
        var bytesRead;
        var buffer;
        var readSize;

        function input(var request) {
            self.init(request);
        }

        function init(var request) {
            self.request = request;
            self.contentReader = dlfunc(null, "mod_parrot_read", "ipip");
            self.headerReader = dlfunc(null, "mod_parrot_headers_in", "PPp");
            self.requestReader = dlfunc(null, "mod_parrot_request_parameters", "PPp");
            self.buffer = new ByteBuffer;
            self.readSize = 16; // this should be configurable 
            self.open(request);
        }
        
        function open(var request) {
            var open = dlfunc(null, "mod_parrot_open_input", "ip");
            self.remaining = open(request);
            self.bytesRead = 0;
        }

        function readline() {
            // until we have a buffering system, do not implement this.
            // it isn't all that sensible, after all
            throw new apache.error(500, 'apache.input.readline() NYI');
        }

        function readall() {
            var builder = new StringBuilder;
            while(self.remaining > 0) {
                string msg = self.read(self.readSize);
                push(builder, msg);
            }
            return string(builder);
        }

        function read(int size) {
            self.buffer =: size;
            var reader = self.contentReader;
            int readBytes = reader(self.buffer, size, self.request);
            if(readBytes > 0) {
                self.remaining -= readBytes;
                self.bytesRead += readBytes;
                return self.buffer.get_chars(0, readBytes, "binary");
            } else {
                self.remaining = 0;
                return "";
            }
        }
        
        function headers() {
            var reader = self.headerReader;
            return reader(getinterp(), self.request);
        }

        function request() {
            var reader = self.requestReader;
            return reader(getinterp(), self.request);
        }
    }

    class error : Exception {
        var code;
        var msg;
        function error(int code, string msg) {
            self.code = code;
            self.msg = msg;
        }

        function report() {
            var out = getstdout();
            if(out instanceof output) {
                out.status(self.code);
            }
            getstderr().print(string(self.msg) + "\n");
        }
    }


    function setup[main](var request) {
        getinterp().stdin_handle(new input(request));
        getinterp().stdout_handle(new output(request));            
    }
}
