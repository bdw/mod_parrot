
$include_const "cclass.pasm";

inline find_cclass(int cclass, string s, int startIdx, int length) return int
{
    int idx;
    ${ find_cclass idx, cclass, s, startIdx, length };
    return idx;
}

inline find_not_cclass(int cclass, string s, int startIdx, int length) return int {
    int idx;
    ${ find_not_cclass idx, cclass, s, startIdx, length };
    return idx;
}

inline cut_number(string s) return int {
    int i = find_cclass(CCLASS_NUMERIC, s, 0, length(s));
    int j = find_not_cclass(CCLASS_NUMERIC, s, i, length(s)-i);
    return int(substr(s, i, j - i));
}

namespace cgi {
    class output : apache.output {
        function puts(string msg) {
            if(self.bytesWritten == 0) {
                var lines = split("\n", msg);
                while(elements(lines) > 0) {
                    string payload = shift_string(lines);
                    int i = indexof(payload, ':');
                    if(indexof(payload, "HTTP") == 0) {
                        string s = substr(payload, 8);
                        self.status(cut_number(s));                                                
                    } else if(i > 0) {
                        // must be a header                    
                        string key = substr(payload, 0, i);
                        string val = substr(payload, i + 1);
                        if(upcase(key) == "STATUS")
                            self.status(cut_number(val));
                        else
                            self.header(key, val);
                    } else { // it is either the end of headers or a
                             // content line
                        if(length(payload) > 0)
                            unshift(lines, payload);
                        break;
                    }
                }
                if(elements(lines) > 0) 
                    self.write(join("\n", lines));
            } else {
                self.write(msg);
            }
        }
    }

    function cgiify(var value) {
        for(int i = 0; i < length(value); i++) {
            i = find_not_cclass(CCLASS_ALPHANUMERIC, value, 
                                i, length(value) - i);
            if(i < length(value))
                value[i] = "_";
        }   
        value = upcase(value);
        if(substr(value, 0, 7) == "CONTENT")
            return value;
        return "HTTP_" + string(value);
    }

	/* i don't know which of those tags does the trick 
	 * but one does */
	function init[anon, load, init]() {
		loadlib('os');
		loadlib('file');
	}
	
    function stop(int code, string msg) {
        throw new apache.error(code, msg);
    }

    function run(string language, string fileName) {
        try {
            var os = new OS;
			var file = new File;
			load_language(language);
			var compiler = compreg(language);
			if(compiler == null) {
                stop(500, 'Cannot load the configured compiler');
			}
			if(!file.exists(fileName)) {
                stop(404, fileName + " not found");
            } 
            if(os.can_read(fileName) && os.can_execute(fileName)) {
				var handle = new FileHandle;
				handle.open(fileName, "r");
				var bytecode = compiler.compile(handle.readall());
				var main = bytecode.main_sub();
				main([ fileName ]);	
			} else {
                stop(403, "Cannot execute " + fileName);
			}	
        } catch(error) {
            try {
                error.report();
            } catch(e) {
                getstderr().print(e.message);
            }
        }
    }

    function main[main](var request, string compiler, string fileName) {
        getinterp().stdin_handle(new apache.input(request));
        getinterp().stdout_handle(new output(request));
        var env = new Env;
        var headers = getstdin().headers();
        var parameters = getstdin().request();
        /* put headers first, we don't want the request variables to be
           overridden */

        for(string k in headers) {
            string value;
            env[cgiify(k)] = headers[k];
        }
        for(string k in parameters) {
            env[k] = parameters[k];
        }

        run(compiler, fileName);
    }

}
